
: dup      0i PICK       ( a -- a a )   ;
: over     1i PICK     ( a x -- a x a ) ;
: swap  2i 1i ROTATE   ( a b -- b a )   ;
: rot   3i 1i ROTATE ( a b c -- b c a ) ;
: ROLL     1i ROTATE ;
: LORR    -1i ROTATE ;

comptime : if
  there           ( -- p0)
  -1i SEW-JMP0    ( forward JMP0 placeholder; targets `else` or `then` )
  (IF box        ( p0 -- p0:IF) )
;

comptime : else
  ( IF unbox          ( p0:IF -- p0) )
  there                ( p0 -- p0 p1 )
  -1i SEW-JMP    ( forward JMP placeholder, targets then )
  there               ( ... -- p0 p1 p2 )
  rot navigate dup    ( ... -- p1 p2 p2)
  SEW-ADDR            ( ... -- p1 p2)
  navigate            ( ... -- p1 )
  ( IF box               ( p1 -- p1:IF) )
;

comptime : then
  ( IF unbox     (p1:IF -- p1) )
  there there rot ( p1 -- t1 t1 p1 )
  navigate  ( t1 t1 p1 -- t1 t1 )
  SEW-ADDR     ( t1 t1 -- t1 )
  navigate        ( t1 -- )
;

: f32
	dup typeof VAL_FLOAT I= if     return then
	dup typeof VAL_INT   I= if I>F return then
	halt ( TODO error message )
;

: i32
	dup typeof VAL_INT   I= if     return then
	dup typeof VAL_FLOAT I= if F>I return then
	halt ( TODO error message )
;

( implement number (i32/f32) input for various words; builtins assume i32 regardless of type)

: pick   i32 PICK ;
: roll   i32 ROLL ;
: lorr   i32 LORR ;
comptime : jmpi  <# i32 JMPI #> ;
: jsri   i32 JSRI ;
comptime : call  <# i32 JSRI #>  ;
: arrget   i32 ARRGET ;
: arrset   swap i32 swap ARRSET ;
: arrsplit i32 ARRSPLIT ;

: assert if else halt then ;

: assert-i32 ( i -- )
	typeof VAL_INT I= assert
;

: box-i32 (i:i32 T -- T(i))
	swap dup assert-i32
	swap CAST
;

: unbox-i32 (T(i) T -- i:i32)
	swap dup typeof ( .. -- T T(i) typeof(T(i)) )
	rot I= assert
	VAL_INT CAST
;

: $NEXT_GLOBAL 0i ;

( initialize comptime vm )
comptime : _init_comptime0
  0i  $NEXT_GLOBAL  SET-GLOBAL
; _init_comptime0

( comptime allocate next global index )
comptime : ALLOC-STATIC-GLOBAL
  $NEXT_GLOBAL  GET-GLOBAL (   -- I )
  dup 1i I+   ( .. -- I (I+1) )
  $NEXT_GLOBAL  SET-GLOBAL ( .. -- I )
  SEW-LIT
;

: $NULL ALLOC-STATIC-GLOBAL ;
: $ERROR_MESSAGE ALLOC-STATIC-GLOBAL ;

( VAL_FLOAT ( comes from preamble ) )

(
24.42 66i CAST
44i 66i box-i32
66i unbox-i32
)


:& boo 366 ;

boo call


